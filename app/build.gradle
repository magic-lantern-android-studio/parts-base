apply plugin: 'com.android.library'
android {
    compileSdkVersion 26
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 26
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
        }
        androidTest {
            resources.srcDirs += ['src/androidTest/resources']
        }
    }

    packagingOptions {
        exclude 'overview-summary.html'
    }
}

dependencies {
    implementation project(':min3d')
    implementation files('libs/mlert.jar')
    implementation files('libs/mlmath.jar')
    // JUnit 4 framework
    testImplementation 'junit:junit:4.12'
}

// Task to delete the old jar.
task deleteOldJar(type: Delete){
    delete 'release/parts.jar'
}

// Task to export contents as jar.
task exportJar(type: Copy){
    from('build/intermediates/packaged-classes/release/')
    into('release/')
    include('classes.jar')
    // Rename the jar.
    rename('classes.jar', 'parts.jar')
}

exportJar.dependsOn(deleteOldJar, build)

// Task to generate jar containing sources
task generateSourcesJar (type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

// Task to delete the old jar
task deleteSourcesJar(type: Delete){
    delete 'libs/parts-sources.jar'
}

task exportSourcesJar(type: Copy){
    from('build/libs/')
    into('release/')
    include('app-sources.jar')
    // Rename the jar.
    rename('app-sources.jar', 'parts-sources.jar')
}

exportSourcesJar.dependsOn(deleteSourcesJar, generateSourcesJar)

artifacts {
    archives generateSourcesJar
}